plugins {
    id 'maven-publish'
    id 'jacoco'
    id 'java-library'
}

group = 'io.github.ihsahn'
version = "${version != 'unspecified' ? version : '0.2-SNAPSHOT'}"

repositories {
    mavenLocal()
    jcenter()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Ihsahn/argbinder")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        argBinder(MavenPublication) {
            from components.java
        }
    }
}

wrapper {
    gradleVersion = "6.3"
}